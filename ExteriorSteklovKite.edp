// Lukas Bundrock, Alexandre Girouard, Denis S. Grebenkov, Michael Levitin, and Iosif Polterovich
// Exterior Steklov problem for the kite via conformal transform
// Script accompanying the paper "The exterior Steklov problem for Euclidean domains"
//
real xmin=-3.5, xmax=3.5, ymin=-3.5, ymax=3.5; // limit of the output region
real t;
int npinner=800; 
int nev=10; 
ofstream fout("external-kite-data-web.txt");

func real xkite(real t) {return 1.5*cos(t)+0.7*cos(2*t)-0.4;}
func real ykite(real t) {return 1.5*sin(t)-0.3*cos(t);}
func real mkite2(real t) {return xkite(t)^2+ykite(t)^2;}

border kitestar(t=2*pi,0) {x = xkite(t)/mkite2(t); y= -ykite(t)/mkite2(t); label=1;}

mesh Th0=buildmesh(kitestar(npinner),nbvx=200000);
plot(Th0, wait=1);
fespace VhIn0(Th0,P2);
VhIn0 v1,v2,v;
varf DomIn0(v1,v2)=int2d(Th0)( dx(v1)*dx(v2) + dy(v1)*dy(v2));
varf BoundIn0(v1,v2)=int1d(Th0,1)(1/(x^2+y^2)*v1*v2);
matrix AIn0= DomIn0(VhIn0,VhIn0,solver=sparsesolver); 
matrix BIn0= BoundIn0(VhIn0,VhIn0); 

real[int] ev(nev);
VhIn0[int] eV(nev); 
int k=EigenValue(AIn0,BIn0,sym=true,sigma=0,value=ev,vector=eV,tol=1e-7,maxit=0,ncv=0);
cout <<"kite conformal eigenvalues\n";
cout.fixed << ev << "\n";
cout << Th0.nv << "\n"; 
fout <<"external kite eigenvalues\n";
fout.fixed << ev << "\n";

int nmesh=0; 
int nmeshfull = Th0.nv;
//cout << "nmeshfull=" << nmeshfull  << endl;
real[int,int] data(nmeshfull,1+nev);

for (int i = 0; i < nmeshfull; i++) {
	real xCoord = Th0(i).x;
	real yCoord = Th0(i).y;
	real xOut = xCoord/(xCoord^2+yCoord^2);
	real yOut = -yCoord/(xCoord^2+yCoord^2);
	if ((xOut>=xmin) && (xOut<=xmax) && (yOut>=ymin) && (yOut<=ymax)) {
		nmesh++;
		data(nmesh-1,0)=xOut;
		data(nmesh-1,1)=yOut;
		for (int ne=1; ne < nev; ne++) {
			data(nmesh-1,ne+1)=eV[ne](xCoord, yCoord);
		}
	} 
}
cout << "nmesh=" << nmesh  << endl;

real[int,int] databox(nmesh,1+nev);
for (int i = 0; i < nmesh; i++) {
	for (int j = 0; j < 1+nev; j++) {
		databox(i,j)=data(i,j);
	}
}

fout << databox << endl;